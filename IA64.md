# Intel 64-bit

These notes are short and sweet and used as a remainder of things I've often
gotten confused about.

The complete set of Intel manuals should always be checked for more details.

- [Intel 64 and IA-32 Architectures Software Developer Manuals](https://www.intel.com/content/www/us/en/developer/articles/technical/intel-sdm.html)

If you need to quickly find what an instruction does you can use the exellent
website by Felix Cloutier found [here](https://www.felixcloutier.com/x86/).


## Architecture Basics

We will mostly focus on the execution environment of 64 bit long mode.

### Registers

There are 83 registers on IA-64, for our purposes we will focus on 24 out
of the 83. The 83 registers can be broken down to *program execution* registers
which are 16 64-bit registers (**General Purpose Registers**) 6 16-bit segment
registers, 1 **RFLAGS** register and `rip` the instruction pointer register.

There are also **FPU** register for floating point operations, the old SIMD
**MMX** registers and the more modern **XMM** registers for that are 128 bit
wide and **YMM** registers that are 256 bit wide.

The segment registers were used in 32 bit mode which used segment memory in
the 64 bit mode which we care about segment registers are always set to 0
except for a few exceptions which are not that important and memory is taken
as a linear address space.

The *program execution* registers are used for general purposes and there are
16 of them `rax`, `rbx`, `rcx`, `rdx`, `rdi`, `rsi`, `rbp`, `rsp`, `r8`, `r9`,
`r10`, `r11`, `r12`, `r13`, `r14` and `r15`.

While you can use them any way you wish (except for **rsp** which changes when
you execute stack instructions) some of those registers have a historical usage

* `rax` : Accumulator for operands (e.g arithmetic, increment, decrements) and results.
* `rbx` : Pointer to data in the data segment (think global variables and such)
* `rcx` : Counter for string, array and loop operations (plays the role of `i` in C)
* `rdx` : I/O pointer used to read and write data from I/O devices.
* `rsi` : Pointer to data pointed at by the DS register, think `src` in `memcpy`.
* `rdi` : Pointer to data pointed at by the ES register, think `dst` in `memcpy`.
* `rsp` : Stack pointer, pointer to data in the SS segment.
* `rbp` : Pointer to data on the stack frame.

Since 64 bit mode doesn't use segmented memory pretty much all the above historical
use cases are redundant unless you're targeting 32 bit machines or writing assembly
for real mode.

The **RFLAGS** register is a 64 bit register used for bit control flags, semantically
equivalent to the **EFLAGS** register (32 bit equivalent) in the sense that the high
bits of **RFLAGS** are reserved and you will only use the low 32 bits.

The **RFLAGS** register contains a group of status flags with a few reserved bits that
you can't use (1, 3, 5, 15 and 22 through 31).

There are special instructions that move flags from and to the `rax` register such as
`PUSHFD` and `POPF`. The **RFLAGS** register state is saved across task context switch
and interrupts.

There are a few flags of interest, the `SIGN FLAG` at bit 7 which is used for signed
arithmetic, the `ZERO FLAG` at bit 6 which is set if the result of a comparison or
arithmetic instruction is 0, the `OVERFLOW FLAG` at bit 11 which indicates arithmetic
overflow for signed integers. The `TRAP FLAG` at bit 8 allows hardware single step
debugging and the `INTERRUPT ENABLE FLAG` is used for interrupt requests.

The details for all flags and their usage can be found in Vol.3 of the Intel manuals
in Chapter 3.

The `rip` register which we haven't discussed is not accessible from software unless
not in the classical sense (you can take control of `rip` by triggering a buffer overflow
for example. But for all intents and purposes it shouldn't be accessible, the register
holds the **offset** of the next instruction to be executed and in 64 bit mode only
it is used for RIP-relative addressing which allows you reference things using `rip`
as the base offset. The `rip` is updated everytime an instruction is executed and is
controlled implictly by control flow instructions such as `CALL` `RET` `JMP`...

## Addressing Modes

Addressing modes are the ways you can address memory using offsets for example for
array accesses or passing structs that hold pointers to fields or to other structs.

There are many possible address modes but they are all based on the following components:

* Displacement: This is an immediate 8, 16 or 32 bit value.
* Base: This is a value stored in a register
* Index: This is a valud stored in a register
* Scale factor: Value of 2,4 or 8 that can be used with Index (2D array access using strides).

The general forumale for using the components is as follows, to get the **effective address**
which is an offset in memory you can compute it as follows.

```
offset = base + index * scale + displacement

```

From this "formula" we can have a combination of possible ways to address memory.

* Displacement: Think absolute addresses e.g 0xfabeffff

* Base : Think a memory address stored in `rax`

* Base + Displacement : Used to index into arrays and struct fields, the base holds the pointer to
  the array start or struct address in memory and the displacement is either fixed (for arrays
  for example 4 in an array of int or depends on the field size which will be padded).

* Index * Scale + Displacement: This is used pretty much in indexing static arrays

* Base + Index + Displacement: This is used for 2 dimensional arrays or an array of structs, where
  the base would be the array address, index moves across records and displacement is an offset to
  the struct fields.

* Base + (Index * Scale) + Displacement : This is used for accessing 2 dimensional arrays where the
  elements are 2,4 or 8 bytes in size (value of scale factor) and is pretty much 1-1 equivalent to
  using strides on a 1-d array.


Lot more details available [here](https://blog.yossarian.net/2020/06/13/How-x86_64-addresses-memory)

## Instruction Encoding


## Performance Monitoring

## The Stack

The hardware stack is a special region of memory where stack semantics apply
i.e `push` and `pop`. In Intel's 64 and 32 bit architectures the stack is
upside down and starts at a high address e.g 0xffffffff, that's why the stack
looks "upside down" because the bottom is at the highest memory address of the
process.

Intel uses a special register `ESP/RSP` used to point at the top of the stack,
this register is called the **stack pointer**, the value in the register **changes
automatically** when you use the `push` & `pop` instructions.

### Stack Operations

You can store things on the stack in two ways :

* `push rax` : Stores the contents of the `rax` register in the stack.
* `sub rsp, 8; mov [rsp], eax` : Make space on the stack by subtracting the number
   of bytes you need and move the value to the memory address pointed at by `rsp`.


For example to store the value `0xDEADBEEF` on the stack (assuming it's stored in
`rax`) :

```asm

sub esp, 4
mov [rsp], eax

```

```asm

push eax

```

```
 ---------- 0xffffffff
|          |
|          |
|          |
|          |
|          |
|          |
|          |
|          |
|          |
|          |
|          |
 ---------- 0xffffaabb  <- ESP ; The top of the stack before our instructions
|   DE     |
|   AD     |
|   BE     |
|   EF     |
 ---------- 0xffffaab7 <- ESP ; The top of the stack after our instructions.

```

### Stack Frames and Calling Conventions

Stack frames are a region in the stack memory that is *local* to a function
call and holds a logical reference to *return* from the call.

The stackframe will usually hold the following :

* Space for arguments beyond the first 7 (The first 7 arguments are passed in registers)
* Space for local variables
* Memory address of the caller
* Registers that should not be clobbered saved by the called function in case they are used (called callee saved)

Calling conventions specify how registers are used, usually each triplet `machine-vendor-os`
will have it's own. For example Intel & AMD 64 bit on Linux for most vendors will support
the [System V ABI](https://refspecs.linuxbase.org/elf/x86_64-abi-0.99.pdf).

For example consider the following snippet and the annotated assembly output following it
start reading the assembly from the `caller`.

You will notice that we didn't *make space for local variables* in callee, that's also an
ABI thing. That's because the 128-byte memory space beyond the location pointer at by `rsp`
i.e the range `[rsp..rsp - 128]` is called the **red zone** and the ABI tells you the following :

```

The 128-byte area beyond the location pointed to by %rsp is considered to be reserved and shall
not be modified by signal or interrupt handlers. Therefore, functions may use this area for temporary
data that is not needed across function calls.

In particular, leaf functions may use this area for their entire stack frame, rather than adjusting
the stack pointer in the prologue and epilogue. This area is known as the red zone.

In this case `callee` doesn't call another function so it's a **leaf function** and can therefore
use the red zone and avoid allocations and restores in the epilogue and prologue.

The **prologue** is basically `push rbp; mov rbp, rsp;` and the **epilogue** is `pop rbp; ret` since
the pattern of using `rbp` (The base register) to find things in the current stack frame is
omnipresent in most x86-64 code the process of saving and restoring `rbp` is global to every
function call you'll see (even empty functions at optimization level 0 i.e `-O0` will have it).

- **Callee-saved** : registers used by the *called function* if it uses them they must be restored
    for the System V ABI (`rbx`, `rbp`, `rsp`, `r12..r15`).

- **Caller-saved** : registers saved by the *calling function* if they are *used after the call*
    if they are not used they don't need to be saved (can be clobbered).

```c

int callee(int a, int b, int c)
{
    int x = a + 2;
    int y = b + 3;
    int z = c + 4;
    int sum = x * y + z;

    return sum;
}

int caller()
{
    return foobar(77, 88, 99);
}

```

```c

callee:
        // This saves the value of `rbp` before `call callee`
        // was executed. This is equivalent to `sub rsp, 8`.
        push    rbp
        // Use the current stack pointer as a reference in `rbp`
        mov     rbp, rsp
        // Store the first argument at the memory address of `[rbp - 20]`
        mov     DWORD PTR [rbp-20], edi
        // Store the second argument at the memory address of `[rbp -24]`
        mov     DWORD PTR [rbp-24], esi
        // Store the last argument at the memory address of `[rbp - 28]`
        mov     DWORD PTR [rbp-28], edx
        // Store 77 in `eax`.
        mov     eax, DWORD PTR [rbp-20]
        // eax = 77 + 2
        add     eax, 2
        // Store eax at `[rbp - 4]`, this is in the redzone.
        mov     DWORD PTR [rbp-4], eax
        // Now move 88 into `eax` to the second computation.
        mov     eax, DWORD PTR [rbp-24]
        add     eax, 3
        // Store back `eax` at `[rbp - 8]` this is also in the redzone.
        mov     DWORD PTR [rbp-8], eax
        // Finally the last computation.
        mov     eax, DWORD PTR [rbp-28]
        add     eax, 4
        // Store back `eax` at `[rbp - 12]`.
        mov     DWORD PTR [rbp-12], eax
        // Now we compute `sum`.
        mov     eax, DWORD PTR [rbp-4]
        imul    eax, DWORD PTR [rbp-8]
        mov     edx, eax
        mov     eax, DWORD PTR [rbp-12]
        add     eax, edx
        mov     DWORD PTR [rbp-16], eax
        // Store `sum` at `eax` which is where the return value is stored.
        mov     eax, DWORD PTR [rbp-16]
        // Restore `rbp` to it's old value.
        pop     rbp
        // Ret uses the address stored at the stop of the stack to return
        // execution there.
        ret
caller:
        // `caller` will use `rbp` so it needs to save it
        // and restore it for the function that calls `caller`.
        push    rbp
        // Store the current value of the stack pointer in `rbp`
        // locally to "move around" its stack frame a function
        // will use `rbp` as a reference point since `rsp` changes
        // as instructions down the line modify the stack.
        mov     rbp, rsp
        // Move the last argument to `edx` this follows the ABI
        mov     edx, 99
        // Move the second from last argument to `esi`
        mov     esi, 88
        // Move the first argument to `edi`.
        mov     edi, 77
        // The call instructions pushes the current instruction pointer
        // to the stack, the instruction pointer points to the instruction
        // **after** `call callee` i.e `pop rbp`.
        // It is equivalent to `push rip; jmp [callee]`
        // From here start reading `callee`.
        call    callee.
        pop     rbp
        ret

```

While the `rbp` register plays the role of a stack frame pointer it can be
omitted and `rsp` relative addressing is used instead.

If we compile the above code using `gcc` with `-fomit-frame-pointer` we end
up with the following :

```asm

callee:
        mov     DWORD PTR [rsp-20], edi
        mov     DWORD PTR [rsp-24], esi
        mov     DWORD PTR [rsp-28], edx
        mov     eax, DWORD PTR [rsp-20]
        add     eax, 2
        mov     DWORD PTR [rsp-4], eax
        mov     eax, DWORD PTR [rsp-24]
        add     eax, 3
        mov     DWORD PTR [rsp-8], eax
        mov     eax, DWORD PTR [rsp-28]
        add     eax, 4
        mov     DWORD PTR [rsp-12], eax
        mov     eax, DWORD PTR [rsp-4]
        imul    eax, DWORD PTR [rsp-8]
        mov     edx, eax
        mov     eax, DWORD PTR [rsp-12]
        add     eax, edx
        mov     DWORD PTR [rsp-16], eax
        mov     eax, DWORD PTR [rsp-16]
        ret
caller:
        mov     edx, 99
        mov     esi, 88
        mov     edi, 77
        call    callee
        ret

```

## Function Calls, Interrupts and Exceptions

Intel IA-64 supports function calls using `call` and `ret` instructions, when
handling function calls we always use the *procedure stack* or *stack* described
in the section above.

Every function call follows the same pattern, where we save the state of the
*calling* function, setup parameters for the *called* function and store local
variables for the *called* function. When a function finishes execution it must
restore the *procedure stack* before transfering control to the calling function.

There is a common terminology used to designate this setup and tear down process
the *prologue* where the stack frame pointer is saved and then over written by
the *stack pointer* followed by allocating space on the *procedure stack* for
local variables, and then there is the *epilogue* which restores the *stack*
to its original state by restoring the old stack frame pointer.

This setup and tear down process can be done in two ways :

* Using stack operations `push` & `pop`
* Using `enter` & `leave` which are instruction aliases for the stack operations.

For example consider the following function :

```c
    int dostuff(int x) {
        int xx;
        long yy;
        long long zz;
        int arr [256];
        // some stuff
        return -1;
}

```

This function compiles to the following prologue and epilogue

```asm
    ; prologue
    push rbp ; save the stack frame pointer of the caller
    mov rbp, rsp ; use the current stack pointer as the frame pointer
    sub rsp, 912 ; allocate space in the stack for the local variables
    ...
    ; epilogue
    leave; restores the old stack frame pointer
    ret ; return to caller

```

There are two things we need to discuss, why did we substract `920` from the stack
when we allocate space for (4 + 4 + 8 + 256 * 4) = 1040 bytes ?

The second is the `leave` instruction.

Let's discuss the first one, as we mentionned before in Intel IA-64 the 128 byte
below the stack pointer is a region called the **red zone** and is guarrenteed
to be left intact by the OS. This means that unless you use more than 128 bytes
of space in a function the CPU will always use the red zone without explicitly
making allocations.

Thus for our 1040 bytes, we substract the space offered by the red zone 128 bytes
we get 912 bytes which is exactly how much we allocated on the stack.

For the `leave` instruction, well this was introduced in i286 and i386 and is
considered to be an alias of `pop rbp` the reference says :

```

The LEAVE instruction copies the frame pointer (in the EBP register) into the stack
pointer register (ESP), which releases the stack space allocated to the stack frame.
The old frame pointer (the frame pointer for the calling procedure that was saved by
the ENTER instruction) is then popped from the stack into the EBP register, restoring
the calling procedureâ€™s stack frame.

```

In our case by using `push rbp; mov rbp, rsp;` we essentially copied the old stack
pointer to `rbp` to use it as an achor to reference stack data within the frame
that means that `rbp` always holds the *old stack pointer* and isn't modified
during the frame. So to restore the stack pointer to the original caller frame
we simply pop it from the stack, that's what `leave` does.

One thing to notice is that the 1040 bytes of data that was written during the
old frame isn't overwritten, after we return from the procedure, the data remains
in the stack until it gets overwritten on the next function call.

In 32 bit mode the stack is aligned on a word (16-bit) or double word (32-bit)
boundary, in 64 bit mode the stack is aligned on a 64-bit boundary. This means
that when you substract or increment the stack pointer you should only do it
in increments or decrements of 8 (since we operate on byte multiples).

If we change the variable declarations in the above example to the following

```c

int dostuff(int x)
{
    int xx;
    short yy;
    long long zz;
    int arr[257];
    // int sum = a + b + c;
    return -1;
}

```

The output for the prologue becomes :

```asm

callee:
        push    rbp
        mov     rbp, rsp
        sub     rsp, 928
        mov     DWORD PTR [rbp-1044], edi
        mov     eax, -1
        leave
        ret
```

The reason for that is with our new declarations we use (1042 bytes) since
4 + 2 + 8 + 257 * 4 = 1042. If we substract the red zone size (128 bytes)
we get 914. **BUT** 914 isn't aligned on 8 bytes (the memory address that
the stack pointer will point to won't be a multiple of 8, you can check that
by diving 914 by 8) so we add padding to the nearest alignment which in this
case is 928.

To check if an address is aligned (which all addresses are otherwise the CPU
throws a hardware exception, a fault and your code will crash) you can cast
a pointer to `uintptr_t` and check if modulo 8 it equals 0.

Another trick with `gcc` similar to `-fomit-frame-pointer` is that you can
force a certain stack alignment with `-mpreferred-stack-boundary=n` where
the alignment is `2^n` the value for `n` must be between 3 (8 bytes) up to
12 (4096 bytes).

Back to `call` and `ret`, the two instructions can run in two modes (*FAR*
and *NEAR*) *FAR* mode is only used when calling functions that are not within
the current program address space, such as OS system calls.

Let's talk about *NEAR* mode (*FAR* mode will require more details I'll let
you check the bible (instruction manuals).

When you execute `call` the following happens :

1. The current value of the `rip` register is pushed into the stack (the return
    address).
2. The offset of the called function is loaded into `rip` and execution continues

Once you're done and call `ret` the following happens :

1. Pops the top of the stack value into `rip` remember when we execute `call`
   we push `rip` then inside the called function we push `rbp` so once we pop
   `rbp` the top of the stack contains `rip`, unless we have more than 8 arguments
   then we substract the stack pointer to free up the space for arguments passed
   in the stack this is done by `ret n` where `n` is the size of arguments that
   were passed in the stack (only necessary if the abi enforces it e.g `__cdecl` on Windows).
2. Resumes execution at the calling procedure.

```c

int manyargs(int a,int b,int c, int d,
     int e, int f, int g, int h,
    int i, int j, int k, int l, int m) {
    return -1;
}

```

```asm

caller:
        push    rbp
        mov     rbp, rsp
        push    13
        push    12
        push    11
        push    10
        push    9
        push    8
        push    7
        mov     r9d, 6
        mov     r8d, 5
        mov     ecx, 4
        mov     edx, 3
        mov     esi, 2
        mov     edi, 1
        call    manyargs
        ; restore rsp by how many bytes we pushed
        ; into the stack this is because the ABI
        ; says that the called function isn't responsible
        ; for cleaning it's stack so the caller does.
        add     rsp, 56
        leave
        ret
```

## The curious case of a stack alignment bug

This is a short story about a bug I encountered that had stuck with me ever since.

I previously mentionned that stack alignment can be in either in word or double
word sizes. But that's not exactly true, in fact the System V AMD64 ABI **mandates**
that the stack alignment is 16 bytes. The reason is that SSE instructions in SIMD
are aligned on 16 bytes boundary and to make vectorization efficient a stack
alignment on 16 bytes is required.

But the interesting thing I didn't know this two years ago when reading the book
*Beginning x64 assembly* in particular on Chapter 15 there's an example on how
to pass arguments to functions when the number of arguments is greater than 6.

The example code can be seen in the Github repo [Chapter 15-
function.asm](https://github.com/Apress/beginning-x64-assembly-programming/blob/master/Chapter%2015/15%20function5/function5.asm)

The snippet is as follow :

```asm

extern printf
section .data
    first	db	"A",0
    second	db	"B",0
    third	db	"C",0
    fourth	db	"D",0
    fifth	db	"E",0
    sixth	db	"F",0
    seventh	db	"G",0
    eighth  db      "H",0
    ninth   db      "I",0
    tenth   db      "J",0
    fmt1	db	"The string is: %s%s%s%s%s%s%s%s%s%s",10,0
    fmt2    db	"PI = %f",10,0
    pi   dq      3.14

section .bss
section .text
    global main
main:
    push rbp
    mov rbp, rsp; for correct debugging

    push tenth  ; now start pushing in
    push ninth  ; reverse order
    push eighth
    push seventh
    push sixth

    mov r9, fifth
    mov r8, fourth
    mov rcx, third
    mov rdx, second
    mov rsi, first  ; the correct registers
    mov rdi,fmt1

    mov rax, 0
    call printf
    add rsp, 48
    movsd xmm0,[pi] ; print a float
    mov rax, 1
    mov rdi, fmt2
    call printf
    leave
    ret

```

When compiling and executing the above code on a 64-bit linux machine it throws
a SIGSEV. *Whoops what is going on ?* I asked myself. So let's throw this code
in `gdb`.

```sh

Reading symbols from many_args...
(gdb) disas main
Dump of assembler code for function main:
   0x0000000000401130 <+0>:     push   rbp
   0x0000000000401131 <+1>:     mov    rbp,rsp
   0x0000000000401134 <+4>:     push   0x404042
   0x0000000000401139 <+9>:     push   0x404040
   0x000000000040113e <+14>:    push   0x40403e
   0x0000000000401143 <+19>:    push   0x40403c
   0x0000000000401148 <+24>:    push   0x40403a
   0x000000000040114d <+29>:    movabs r9,0x404038
   0x0000000000401157 <+39>:    movabs r8,0x404036
   0x0000000000401161 <+49>:    movabs rcx,0x404034
   0x000000000040116b <+59>:    movabs rdx,0x404032
   0x0000000000401175 <+69>:    movabs rsi,0x404030
   0x000000000040117f <+79>:    movabs rdi,0x404044
   0x0000000000401189 <+89>:    mov    eax,0x0
   0x000000000040118e <+94>:    call   0x401030 <printf@plt>
   0x0000000000401193 <+99>:    add    rsp,0x30
   0x0000000000401197 <+103>:   movsd  xmm0,QWORD PTR ds:0x404072
   0x00000000004011a0 <+112>:   mov    eax,0x1
   0x00000000004011a5 <+117>:   movabs rdi,0x404069
   0x00000000004011af <+127>:   call   0x401030 <printf@plt>
   0x00000000004011b4 <+132>:   leave
   0x00000000004011b5 <+133>:   ret
End of assembler dump.
(gdb)

```

Well that doesn't look interesting, hmm let's check what happens if we run
this code.

```sh

(gdb) r
Starting program: /home/kafka/Projects/snippets_of_asm/examples/many_args
[Thread debugging using libthread_db enabled]
Using host libthread_db library "/lib/x86_64-linux-gnu/libthread_db.so.1".

Program received signal SIGSEGV, Segmentation fault.
0x00007ffff7c750d0 in __vfprintf_internal (s=0x7ffff7e1a780 <_IO_2_1_stdout_>, format=0x404044 <fmt1> "The string is: %s%s%s%s%s%s%s%s%s%s\n", ap=ap@entry=0x7fffffffddf8, mode_flags=mode_flags@entry=0) at ./stdio-common/vfprintf-internal.c:1244
1244    ./stdio-common/vfprintf-internal.c: No such file or directory.
(gdb)

```

Okay now we're getting somewhere, let's put a breakpoint before the call
to `printf`.

```

End of assembler dump.
(gdb) b *main + 94
Breakpoint 2 at 0x40118e
(gdb) next
Single stepping until exit from function main,
which has no line number information.

Breakpoint 2, 0x000000000040118e in main ()
(gdb) disas main
Dump of assembler code for function main:
   0x0000000000401130 <+0>:     push   rbp
   0x0000000000401131 <+1>:     mov    rbp,rsp
   0x0000000000401134 <+4>:     push   0x404042
   0x0000000000401139 <+9>:     push   0x404040
   0x000000000040113e <+14>:    push   0x40403e
   0x0000000000401143 <+19>:    push   0x40403c
   0x0000000000401148 <+24>:    push   0x40403a
   0x000000000040114d <+29>:    movabs r9,0x404038
   0x0000000000401157 <+39>:    movabs r8,0x404036
   0x0000000000401161 <+49>:    movabs rcx,0x404034
   0x000000000040116b <+59>:    movabs rdx,0x404032
   0x0000000000401175 <+69>:    movabs rsi,0x404030
   0x000000000040117f <+79>:    movabs rdi,0x404044
   0x0000000000401189 <+89>:    mov    eax,0x0
=> 0x000000000040118e <+94>:    call   0x401030 <printf@plt>
   0x0000000000401193 <+99>:    add    rsp,0x30
   0x0000000000401197 <+103>:   movsd  xmm0,QWORD PTR ds:0x404072
   0x00000000004011a0 <+112>:   mov    eax,0x1
   0x00000000004011a5 <+117>:   movabs rdi,0x404069
   0x00000000004011af <+127>:   call   0x401030 <printf@plt>
   0x00000000004011b4 <+132>:   leave
   0x00000000004011b5 <+133>:   ret
End of assembler dump.
(gdb)

```

Ok let's check the register state and see what's going on there

```

(gdb) i r
rax            0x0                 0
rbx            0x0                 0
rcx            0x404034            4210740
rdx            0x404032            4210738
rsi            0x404030            4210736
rdi            0x404044            4210756
rbp            0x7fffffffdf00      0x7fffffffdf00
rsp            0x7fffffffded8      0x7fffffffded8
r8             0x404036            4210742
r9             0x404038            4210744
r10            0x7ffff7fc3908      140737353890056
r11            0x7ffff7fde660      140737353999968
r12            0x7fffffffe018      140737488347160
r13            0x401130            4198704
r14            0x403e18            4210200
r15            0x7ffff7ffd040      140737354125376
rip            0x40118e            0x40118e <main+94>
eflags         0x246               [ PF ZF IF ]
cs             0x33                51
ss             0x2b                43
ds             0x0                 0
es             0x0                 0
fs             0x0                 0
gs             0x0                 0
(gdb)

```

Do you see it ? I'll give you a hint if the stack is aligned on the proper
boundary 16 bytes then the address where `rsp` points should end in 0.

I didn't see it at first so I went and wrote the equivalent code in C
and ran it through objdump.

```c

void calls_printf() {
    printf("%s%s%s%s%s%s%s%s%s", "a","b","c","d", "e","f","g","h","i","j");
}

```

The output (I omitted the constants)

```asm

calls_printf():
        push    rbp
        mov     rbp, rsp
        sub     rsp, 8
        push    OFFSET FLAT:.LC6
        push    OFFSET FLAT:.LC7
        push    OFFSET FLAT:.LC8
        push    OFFSET FLAT:.LC9
        push    OFFSET FLAT:.LC10
        mov     r9d, OFFSET FLAT:.LC0
        mov     r8d, OFFSET FLAT:.LC1
        mov     ecx, OFFSET FLAT:.LC2
        mov     edx, OFFSET FLAT:.LC3
        mov     esi, OFFSET FLAT:.LC4
        mov     edi, OFFSET FLAT:.LC5
        mov     eax, 0
        call    printf
        add     rsp, 48
        nop
        leave
        ret
```

Look at it sitting right there `sub rsp, 8` was what we needed in the code
and that's what happens when you don't pay attention when reading the ABI.

The story doesn't end here of course, by that point I didn't know why `sub rsp, 8`
was there so I went and looked for an explanation try googling "Why sub rsp, 8"
I wasn't the first :). After much search I found a satisfying explanation.

[Satisfying explanation for why `sub rsp,
8`](https://stackoverflow.com/questions/51070716/glibc-scanf-segmentation-faults-when-called-from-a-function-that-doesnt-align-r)

The reason you do `sub rsp, 8` or you can always do `push rax` is that
`call` pushes the return address (8 bytes on the stack) so when the called
function gets control, `rsp` is not aligned and you have to align it
yourself.

The book errata doesn't mention this issue and I haven't found much people
discussing this but here it is in case you encounter this again.

> ALWAYS ALIGN THE STACK

## Quickly checking instruction encoding

Reading the Intel Software Developer Manual Vol.2 is quite exhausting at first
one trick to check if you have the correct encoding for a given instruction is
to use `nasm` and `ndisasm` to assemble the encoding and then disassemble it to
validate if it matches the instruction you had in mind.

```sh

$ cat > foo.asm
db 0x03, 0x54, 0x8b, 0x15

$ nasm foo.asm

$ file foo.asm
foo: data

$ ndisasm -b64 foo
00000000 03548B15 add edx, [rbx+rcx*4+0x15]

```
